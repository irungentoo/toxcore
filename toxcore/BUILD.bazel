load("@rules_cc//cc:defs.bzl", "cc_test")
load("//tools:no_undefined.bzl", "cc_library")

package(features = ["layering_check"])

exports_files(
    srcs = ["tox.h"],
    visibility = ["//c-toxcore:__pkg__"],
)

cc_library(
    name = "attributes",
    hdrs = ["attributes.h"],
    visibility = ["//c-toxcore:__subpackages__"],
)

cc_library(
    name = "bin_pack",
    srcs = ["bin_pack.c"],
    hdrs = ["bin_pack.h"],
    visibility = ["//c-toxcore:__subpackages__"],
    deps = [
        ":ccompat",
        "@msgpack-c",
    ],
)

cc_library(
    name = "bin_unpack",
    srcs = ["bin_unpack.c"],
    hdrs = ["bin_unpack.h"],
    visibility = ["//c-toxcore:__subpackages__"],
    deps = [
        ":ccompat",
        "@msgpack-c",
    ],
)

cc_library(
    name = "ccompat",
    srcs = ["ccompat.c"],
    hdrs = ["ccompat.h"],
    visibility = ["//c-toxcore:__subpackages__"],
    deps = [":attributes"],
)

cc_library(
    name = "crypto_core",
    srcs = ["crypto_core.c"],
    hdrs = ["crypto_core.h"],
    visibility = ["//c-toxcore:__subpackages__"],
    deps = [
        ":ccompat",
        "@libsodium",
    ],
)

cc_test(
    name = "crypto_core_test",
    size = "small",
    srcs = ["crypto_core_test.cc"],
    flaky = True,
    deps = [
        ":crypto_core",
        "@com_google_googletest//:gtest",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_library(
    name = "list",
    srcs = ["list.c"],
    hdrs = ["list.h"],
    deps = [":ccompat"],
)

cc_test(
    name = "list_test",
    size = "small",
    srcs = ["list_test.cc"],
    deps = [
        ":list",
        "@com_google_googletest//:gtest",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_library(
    name = "logger",
    srcs = ["logger.c"],
    hdrs = ["logger.h"],
    visibility = [
        "//c-toxcore/auto_tests:__pkg__",
        "//c-toxcore/other:__pkg__",
        "//c-toxcore/other/bootstrap_daemon:__pkg__",
        "//c-toxcore/toxav:__pkg__",
    ],
    deps = [":ccompat"],
)

cc_library(
    name = "state",
    srcs = ["state.c"],
    hdrs = ["state.h"],
    deps = [":logger"],
)

cc_library(
    name = "mono_time",
    srcs = ["mono_time.c"],
    hdrs = ["mono_time.h"],
    visibility = [
        "//c-toxcore/auto_tests:__pkg__",
        "//c-toxcore/other:__pkg__",
        "//c-toxcore/other/bootstrap_daemon:__pkg__",
        "//c-toxcore/testing:__pkg__",
        "//c-toxcore/toxav:__pkg__",
    ],
    deps = [
        ":ccompat",
        "@pthread",
    ],
)

cc_test(
    name = "mono_time_test",
    size = "small",
    srcs = ["mono_time_test.cc"],
    deps = [
        ":mono_time",
        "@com_google_googletest//:gtest",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_library(
    name = "network",
    srcs = [
        "network.c",
        "util.c",
    ],
    hdrs = [
        "network.h",
        "util.h",
    ],
    visibility = [
        "//c-toxcore/auto_tests:__pkg__",
        "//c-toxcore/other:__pkg__",
        "//c-toxcore/other/bootstrap_daemon:__pkg__",
        "//c-toxcore/toxav:__pkg__",
    ],
    deps = [
        ":ccompat",
        ":crypto_core",
        ":logger",
        ":mono_time",
        "@libsodium",
        "@psocket",
        "@pthread",
    ],
)

cc_test(
    name = "network_test",
    size = "small",
    srcs = ["network_test.cc"],
    deps = [
        ":network",
        "@com_google_googletest//:gtest",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_test(
    name = "util_test",
    size = "small",
    srcs = ["util_test.cc"],
    deps = [
        ":crypto_core",
        ":network",
        "@com_google_googletest//:gtest",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_library(
    name = "ping_array",
    srcs = ["ping_array.c"],
    hdrs = ["ping_array.h"],
    deps = [
        ":crypto_core",
        ":mono_time",
        ":network",
    ],
)

cc_test(
    name = "ping_array_test",
    size = "small",
    srcs = ["ping_array_test.cc"],
    deps = [
        ":mono_time",
        ":ping_array",
        "@com_google_googletest//:gtest",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_library(
    name = "LAN_discovery",
    srcs = ["LAN_discovery.c"],
    hdrs = ["LAN_discovery.h"],
    visibility = [
        "//c-toxcore/other:__pkg__",
        "//c-toxcore/other/bootstrap_daemon:__pkg__",
        "//c-toxcore/testing:__pkg__",
    ],
    deps = [
        ":ccompat",
        ":crypto_core",
        ":network",
    ],
)

cc_library(
    name = "DHT",
    srcs = [
        "DHT.c",
        "ping.c",
    ],
    hdrs = [
        "DHT.h",
        "ping.h",
    ],
    visibility = [
        "//c-toxcore/other:__pkg__",
        "//c-toxcore/other/bootstrap_daemon:__pkg__",
        "//c-toxcore/testing:__pkg__",
    ],
    deps = [
        ":LAN_discovery",
        ":crypto_core",
        ":logger",
        ":mono_time",
        ":network",
        ":ping_array",
        ":state",
    ],
)

cc_test(
    name = "DHT_test",
    size = "small",
    srcs = ["DHT_test.cc"],
    deps = [
        ":DHT",
        ":crypto_core",
        "@com_google_googletest//:gtest",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_library(
    name = "onion",
    srcs = ["onion.c"],
    hdrs = ["onion.h"],
    visibility = ["//c-toxcore/auto_tests:__pkg__"],
    deps = [
        ":DHT",
        ":crypto_core",
        ":mono_time",
        ":network",
    ],
)

cc_library(
    name = "TCP_common",
    srcs = ["TCP_common.c"],
    hdrs = ["TCP_common.h"],
    visibility = ["//c-toxcore/auto_tests:__pkg__"],
    deps = [":network"],
)

cc_library(
    name = "TCP_server",
    srcs = ["TCP_server.c"],
    hdrs = ["TCP_server.h"],
    copts = select({
        "//tools/config:linux": ["-DTCP_SERVER_USE_EPOLL=1"],
        "//conditions:default": [],
    }),
    visibility = [
        "//c-toxcore/auto_tests:__pkg__",
        "//c-toxcore/other:__pkg__",
        "//c-toxcore/other/bootstrap_daemon:__pkg__",
    ],
    deps = [
        ":TCP_common",
        ":crypto_core",
        ":list",
        ":mono_time",
        ":network",
        ":onion",
    ],
)

cc_library(
    name = "TCP_client",
    srcs = ["TCP_client.c"],
    hdrs = ["TCP_client.h"],
    visibility = ["//c-toxcore/auto_tests:__pkg__"],
    deps = [
        ":TCP_common",
        ":mono_time",
        ":network",
    ],
)

cc_library(
    name = "TCP_connection",
    srcs = ["TCP_connection.c"],
    hdrs = ["TCP_connection.h"],
    visibility = ["//c-toxcore/auto_tests:__pkg__"],
    deps = [
        ":DHT",
        ":TCP_client",
        ":TCP_common",
        ":crypto_core",
        ":list",
        ":mono_time",
        ":network",
        ":onion",
    ],
)

cc_test(
    name = "TCP_connection_test",
    size = "small",
    srcs = ["TCP_connection_test.cc"],
    deps = [
        ":TCP_connection",
        "@com_google_googletest//:gtest",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_library(
    name = "net_crypto",
    srcs = ["net_crypto.c"],
    hdrs = ["net_crypto.h"],
    visibility = ["//c-toxcore/auto_tests:__pkg__"],
    deps = [
        ":DHT",
        ":TCP_connection",
        ":list",
        ":mono_time",
        ":network",
    ],
)

cc_library(
    name = "onion_announce",
    srcs = ["onion_announce.c"],
    hdrs = ["onion_announce.h"],
    visibility = [
        "//c-toxcore/auto_tests:__pkg__",
        "//c-toxcore/other/bootstrap_daemon:__pkg__",
    ],
    deps = [
        ":DHT",
        ":LAN_discovery",
        ":mono_time",
        ":network",
        ":onion",
    ],
)

cc_library(
    name = "onion_client",
    srcs = ["onion_client.c"],
    hdrs = ["onion_client.h"],
    visibility = ["//c-toxcore/auto_tests:__pkg__"],
    deps = [
        ":DHT",
        ":LAN_discovery",
        ":mono_time",
        ":net_crypto",
        ":network",
        ":onion_announce",
    ],
)

cc_library(
    name = "friend_connection",
    srcs = ["friend_connection.c"],
    hdrs = ["friend_connection.h"],
    visibility = ["//c-toxcore/auto_tests:__pkg__"],
    deps = [
        ":DHT",
        ":mono_time",
        ":net_crypto",
        ":network",
        ":onion_client",
    ],
)

cc_test(
    name = "friend_connection_test",
    size = "small",
    srcs = ["friend_connection_test.cc"],
    deps = [
        ":friend_connection",
        "@com_google_googletest//:gtest",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_library(
    name = "friend_requests",
    srcs = ["friend_requests.c"],
    hdrs = ["friend_requests.h"],
    visibility = [
        "//c-toxcore/auto_tests:__pkg__",
        "//c-toxcore/other:__pkg__",
        "//c-toxcore/testing:__pkg__",
    ],
    deps = [
        ":friend_connection",
        ":network",
    ],
)

cc_library(
    name = "Messenger",
    srcs = ["Messenger.c"],
    hdrs = ["Messenger.h"],
    visibility = [
        "//c-toxcore/auto_tests:__pkg__",
        "//c-toxcore/testing:__pkg__",
        "//c-toxcore/toxav:__pkg__",
    ],
    deps = [
        ":TCP_server",
        ":friend_requests",
        ":logger",
        ":mono_time",
        ":network",
        ":state",
    ],
)

cc_library(
    name = "group",
    srcs = ["group.c"],
    hdrs = ["group.h"],
    visibility = ["//c-toxcore/toxav:__pkg__"],
    deps = [
        ":Messenger",
        ":mono_time",
        ":network",
        ":state",
    ],
)

cc_library(
    name = "tox",
    srcs = [
        "tox.c",
        "tox_api.c",
    ],
    hdrs = [
        "tox.h",
        "tox_private.h",
    ],
    visibility = ["//c-toxcore:__subpackages__"],
    deps = [
        ":Messenger",
        ":ccompat",
        ":group",
        ":logger",
        ":mono_time",
        ":network",
        "//c-toxcore/toxencryptsave:defines",
    ],
)

cc_test(
    name = "tox_test",
    size = "small",
    srcs = ["tox_test.cc"],
    deps = [
        ":crypto_core",
        ":tox",
        "@com_google_googletest//:gtest",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_library(
    name = "tox_unpack",
    srcs = ["tox_unpack.c"],
    hdrs = ["tox_unpack.h"],
    visibility = ["//c-toxcore:__subpackages__"],
    deps = [
        ":bin_unpack",
        ":ccompat",
        ":tox",
        "@msgpack-c",
    ],
)

cc_library(
    name = "tox_events",
    srcs = ["tox_events.c"] + glob([
        "events/*.c",
        "events/*.h",
    ]),
    hdrs = ["tox_events.h"],
    visibility = ["//c-toxcore:__subpackages__"],
    deps = [
        ":bin_pack",
        ":bin_unpack",
        ":ccompat",
        ":tox",
        ":tox_unpack",
        "@msgpack-c",
    ],
)

cc_library(
    name = "tox_dispatch",
    srcs = ["tox_dispatch.c"],
    hdrs = ["tox_dispatch.h"],
    visibility = ["//c-toxcore:__subpackages__"],
    deps = [
        ":ccompat",
        ":tox_events",
    ],
)

alias(
    name = "toxcore",
    actual = ":tox_dispatch",
    visibility = ["//c-toxcore:__subpackages__"],
)

sh_library(
    name = "cimple_files",
    srcs = glob([
        "events/*.c",
        "events/*.h",
        "*.c",
        "*.h",
    ]),
    visibility = ["//c-toxcore/testing:__pkg__"],
)
